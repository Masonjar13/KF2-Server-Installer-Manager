#singleInstance force
#persistent
#include *i <Lib_1>

global steamcmdUrl,baseDir,serverDir
steamcmdUrl:="http://media.steampowered.com/installer/steamcmd.zip"
baseDir:="steamcmd"
serverDir:=baseDir . "\kf2server"

checkInstall()
exitApp

downloadSteamCmd(){
    urlDownloadToFile(steamcmdUrl)
    smartZip("steamcmd.zip",baseDir)
    fileDelete,steamcmd.zip
}

checkInstall(){
    if(!fileExist(baseDir . "\steamcmd.exe"))
        downloadSteamCmd()
    downloadKF2S()
}

downloadKF2S(){
    runWait,% comspec . " /c start " . basedir . "\steamcmd.exe "
        . "+login anonymous "
        . "+force_install_dir kf2server "
        . "+app_update 232130 "
        . "+quit"
    setWebAdmin()
}

setWebAdmin(){
    iniWrite,true,% baseDir . "\kf2server\KFGame\Config\KFWeb.ini",IpDrv.WebServer,bEnabled
}

urlDownloadToFile(url,fileDest="",method=0){
    if(a_batchLines!=-1){
        pBL:=a_batchLines
        setBatchLines -1
    }
    if(!fileDest){
        splitPath,url,fileDest
        fileDest:=a_scriptDir "\" fileDest
    }
    hObject:=ComObjCreate("WinHttp.WinHttpRequest.5.1")
    hObject.Open("GET",url)
    hObject.Send()
    
    uBytes:=hObject.ResponseBody,cLen:=uBytes.maxIndex()
    fileHandle:=fileOpen(fileDest,"w")

; put to var, write once
    if(method){
        varSetCapacity(f,cLen,0)
        loop % cLen+1
            numPut(uBytes[a_index-1],f,a_index-1,"UChar")
        err:=fileHandle.RawWrite(f,cLen+1)
    }

; skip var, write many times
    else{
        loop % cLen+1 
            err+=fileHandle.WriteUChar(uBytes[a_index-1])
        fileHandle.Close()
    }
    if(pBL)
        setBatchLines % pBL
    return err
}


; http://www.autohotkey.com/forum/viewtopic.php?p=523649#523649

SmartZip(s, o, t = 4)
{
	IfNotExist, %s%
		return, -1        ; The souce is not exist. There may be misspelling.
	
	oShell := ComObjCreate("Shell.Application")
	
	if (SubStr(o, -3) = ".zip")	; Zip
	{
		IfNotExist, %o%        ; Create the object ZIP file if it's not exist.
			CreateZip(o)
		
		Loop, %o%, 1
			sObjectLongName := A_LoopFileLongPath

		oObject := oShell.NameSpace(sObjectLongName)
		
		Loop, %s%, 1
		{
			if (sObjectLongName = A_LoopFileLongPath)
			{
				continue
			}
			ToolTip, Zipping %A_LoopFileName% ..
			oObject.CopyHere(A_LoopFileLongPath, t)
			SplitPath, A_LoopFileLongPath, OutFileName
			Loop
			{
				oObject := "", oObject := oShell.NameSpace(sObjectLongName)	; This doesn't affect the copyhere above.
				if oObject.ParseName(OutFileName)
					break
			}
		}
		ToolTip
	}
	else if InStr(FileExist(o), "D") or (!FileExist(o) and (SubStr(s, -3) = ".zip"))	; Unzip
	{
		if !o
			o := A_ScriptDir        ; Use the working dir instead if the object is null.
		else IfNotExist, %o%
			FileCreateDir, %o%
		
		Loop, %o%, 1
			sObjectLongName := A_LoopFileLongPath
		
		oObject := oShell.NameSpace(sObjectLongName)
		
		Loop, %s%, 1
		{
			oSource := oShell.NameSpace(A_LoopFileLongPath)
			oObject.CopyHere(oSource.Items, t)
		}
	}
}

CreateZip(n)	; Create empty Zip file
{
	ZIPHeader1 := "PK" . Chr(5) . Chr(6)
	VarSetCapacity(ZIPHeader2, 18, 0)
	ZIPFile := FileOpen(n, "w")
	ZIPFile.Write(ZIPHeader1)
	ZIPFile.RawWrite(ZIPHeader2, 18)
	ZIPFile.close()
}
